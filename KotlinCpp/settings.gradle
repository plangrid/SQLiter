import org.jetbrains.kotlin.DistroHelper
import org.jetbrains.kotlin.DistroHelperKt

/*
 * Copyright 2010-2017 JetBrains s.r.o.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

enableFeaturePreview('GRADLE_METADATA')
include ':knarch'

String konanPath = null

if (!rootProject.hasProperty('konanDevPath')) {
    File buildDir = new File(rootProject.getProjectDir(), "build")
    File distFolder = new File(buildDir, "kndist")
    
    if (!distFolder.exists()) {
        File zipFile = new File(buildDir, "kndist.zip")
        File unzipFolder = new File(buildDir, "distextract")
        
        DistroHelperKt.downloadKotlinNativeDistro(kotlinNativeArchive, zipFile)
        DistroHelperKt.unzipFileToFolder(zipFile, unzipFolder)
        DistroHelperKt.distroPath(unzipFolder).renameTo(distFolder)
    }
    
    konanPath = distFolder.path
} else {
    konanPath = konanDevPath
}

include ':dependencies'
project(':dependencies').projectDir = new File(new File(konanPath), "dependencies")

includeBuild "${konanPath}/shared"
includeBuild "${konanPath}/tools/kotlin-native-gradle-plugin"

if (hasProperty("kotlinProjectPath")) {
    includeBuild(kotlinProjectPath) {
        dependencySubstitution {
            substitute module('org.jetbrains.kotlin:kotlin-compiler') with project(':include:kotlin-compiler')
        }
    }
}
